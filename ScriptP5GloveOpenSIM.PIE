/**
  * OpenSim Script Glove Control (P5 Glove)
  * @author: Julio Adrián Hernández Méndez
  * @since: January 15 '14
  * Last update: March 12 '14
  * @version 2.1
  *
  * Script hecho en GlovePIE que permite la manipulación de objetos
  * con ayuda del control P5 Glove.
  *
*/

/*** Debug Area ***/
//debug = Smooth(MapRange(Glove1.AbsoluteY*0.07, 10 inches,-10 inches, 0,1))
//debug = " - RelativeY =" +Glove1.RelativeY
//debug = glove1.roll

/*** Declaracion de variables ***/
var.precisionX = 0.07        //Entre menor sea el numero, mayor precisión
var.precisionY = 0.07        //Entre menor sea el numero, mayor precisión
var.mitadPantalla = 0.5
var.minimoIndice = 30
var.minimoMedio = 30
var.minimoAnular = 25
var.minimoMenique = 25
var.gradosModoGiro = 150

/*** Script ***/
/**
  * Antes de entrar a tomar el objeto el guante funciona como Mouse
  */
Glove1.MouseMode = true
release(Keys.Shift)
release(Keys.Ctrl)
/**
  * Mientras que la mano no se haga puño no entrará aquí
  * No se tomó en cuenta el dedo pulgar porque hace función de click derecho
  */
if(Glove1.Index > var.minimoIndice and Glove1.Middle > var.minimoMedio and Glove1.Ring > var.minimoAnular and Glove1.Pinky > var.minimoMenique) then
    Glove1.MouseMode = false

    if(Glove1.roll < var.gradosModoGiro ) then
        release(Keys.Shift)
        release(Keys.Ctrl)
        Mouse.x = Smooth(MapRange(Glove1.AbsoluteX*var.precisionX, -10 inches,10 inches, 0,1))
        Mouse.Y = var.mitadPantalla
        /**
          * Esta linea siguiente debería hacer posible el movimiento en Z
          * Sin embargo es muy inestable, por lo que opté en dejarlo fijo.
          *
          * Mouse.Y = Smooth(Smooth(MapRange(Glove1.absoluteZ*.1, -40 ,-500, 0,1))) *.2  + .45
        */
        if(Glove1.RelativeY > 200 or Glove1.RelativeY < -200) then
            press(Keys.Ctrl)
            release(Keys.Shift)
            Mouse.x = Smooth(MapRange(Glove1.AbsoluteX*var.precisionX, -10 inches,10 inches, 0,1))
            Mouse.y = Smooth(MapRange(Glove1.AbsoluteY*var.precisionY, 10 inches,-10 inches, 0,1))
        endif
    else
        /**
          * Si la mano gira a la derecha en 150° entrará aquí,
          * que es para poder girar los objetos.
          * Mueva la mano a la derecha o a la izquierda,
          * mientras su mano permanece a más de 150°, para lograrlo
        */
        press(Keys.Shift)
        press(Keys.Ctrl)
        Mouse.x = Smooth(MapRange(Glove1.AbsoluteX*var.precisionX, -10 inches,10 inches, 0,1))
        Mouse.y = Smooth(MapRange(Glove1.AbsoluteY*var.precisionY, 10 inches,-10 inches, 0,1))
    endif
endif  //Fin modo tomar objetos
